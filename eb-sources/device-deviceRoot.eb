#----------------------------------------------------------------------------*

graphic entity DeviceRoot {
#--- Selected page index
  property Int selectedPageIndex default 0 ;

#--- Title
  property @signature String mTitle default "" ;

#--- Image
  property @signature Data mImageData default empty ;
  transient Bool imageIsValid dependsFrom self.mImageData ;

#--- Documentation
  toMany @signature DeviceDocumentation mDocs ;

#--- Prefix
  property @signature String mPrefix default "" ;

#--- Comment
  property @signature String mComments default "" ;

#--- Issues
  transient CanariIssueArray issues dependsFrom
    self.mTitle, self.mPrefix,
    self.inconsistentPackagePadNameSetsMessage,
    self.inconsistentSymbolNameSetMessage,
    self.unconnectedPins,
    self.unconnectedPads,
    self.mPackages.all.mVersion,
    self.mPackages.all.mName,
    self.mSymbolTypes.all.mVersion,
    self.mSymbolTypes.all.mTypeName
  ;

  transient String inconsistentPackagePadNameSetsMessage dependsFrom self.mPackages.all.padNameSet, self.mPackages.all.mName ;
  transient Bool packagePadNameSetsAreConsistent dependsFrom self.mPackages.all.padNameSet ;

  transient String inconsistentSymbolNameSetMessage dependsFrom self.mSymbolInstances.all.qualifiedName ;
  transient Bool symbolNameAreConsistent dependsFrom self.inconsistentSymbolNameSetMessage ;

#--- Packages
  toMany @signature PackageInDevice mPackages inverse toOne mRoot ;
  property @signature Int mPackageDisplayZoom default 100 ;
  property @signature Bool mPackageDisplayHorizontalFlip default no ;
  property @signature Bool mPackageDisplayVerticalFlip default no ;
  property @signature Bool mShowPackages default yes ;
  property @signature Bool mShowPackagePadNumbers default yes ;
  property @signature Bool mShowPackageFrontPads default yes ;
  property @signature Bool mShowPackageBackPads default yes ;
  
#--- Symbols
  toMany @signature SymbolTypeInDevice mSymbolTypes ;
  toMany @signature SymbolInstanceInDevice mSymbolInstances ;

  property @signature Int mSymbolDisplayZoom default 100 ;
  property @signature Bool mSymbolDisplayHorizontalFlip default no ;
  property @signature Bool mSymbolDisplayVerticalFlip default no ;
  
  transient StringArray symbolTypeNames dependsFrom self.mSymbolTypes.all.mTypeName ;
  
  transient UnconnectedSymbolPinsInDevice unconnectedPins dependsFrom self.mSymbolInstances.all.unconnectedPins ;

#--- Pad proxys
  toMany @signature PadProxyInDevice mPadProxies ;
  
  transient StringArray unconnectedPads
    dependsFrom self.mPadProxies.all.mPadName, self.mPadProxies.all.isConnected ;

  transient AssignedPadProxiesInDevice assignedPadProxies dependsFrom
    self.mPadProxies.all.mPadName,
    self.mPadProxies.all.symbolName,
    self.mPadProxies.all.pinInstanceName,
    self.mPadProxies.all.isConnected ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity DeviceDocumentation {

  property @signature String mFileName default "" ;
  property @signature Data mFileData default empty ;
  
  transient Int fileSize dependsFrom self.mFileData ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

graphic entity PackageInDevice {
  toOne DeviceRoot mRoot inverse toMany mPackages ;
  
  toMany @signature MasterPadInDevice mMasterPads ;
 
  property @signature Data mFileData default empty ;
  property @signature String mName default "" ;
  property @signature Int mVersion default 0 ;
  
  property @signature NSBezierPath mStrokeBezierPath default empty ;
 
  property @signature Int mX default 0 ;
  property @signature Int mY default 0 ;
  
  transient String versionString dependsFrom self.mVersion ;

  transient BezierPathArray frontSidePadFilledBezierPathArray dependsFrom
    self.mMasterPads.all.frontSideFilledBezierPathArray
  ;

  transient BezierPathArray backSidePadFilledBezierPathArray dependsFrom
    self.mMasterPads.all.backSideFilledBezierPathArray
  ;

  transient EBShape objectDisplay dependsFrom
     self.mMasterPads.all.padNumberDisplay,
     self.mRoot.one.mShowPackagePadNumbers,
     self.mRoot.one.mShowPackages,
     self.mRoot.one.mShowPackageFrontPads,
     self.mRoot.one.mShowPackageBackPads,
     self.mStrokeBezierPath,
     prefs.packageColor,
     prefs.packageDrawingWidthMultipliedByTen,
     self.frontSidePadFilledBezierPathArray,
     prefs.frontSidePadColor,
     self.backSidePadFilledBezierPathArray,
     prefs.backSidePadColor,
     self.mName, self.mX, self.mY
  ;

  transient EBShape selectionDisplay dependsFrom
     self.mStrokeBezierPath,
     prefs.packageDrawingWidthMultipliedByTen,
     self.frontSidePadFilledBezierPathArray,
     self.backSidePadFilledBezierPathArray,
     self.mName, self.mX, self.mY
  ;
  
  transient StringSet padNameSet dependsFrom self.mMasterPads.all.mName ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity MasterPadInDevice {
  toMany @signature SlavePadInDevice mSlavePads inverse toOne mMasterPad ;

  property @signature Int mCenterX default 0 ;
  property @signature Int mCenterY default 0 ;
  property @signature Int mWidth default 114_300 ;
  property @signature Int mHeight default 228_600 ;
  property @signature Int mHoleDiameter default 57_150 ;
  property @signature PadShape mShape default octo ;
  property @signature PadStyle mStyle default traversing ;
  property @signature String mName default "" ;
  
  transient BezierPathArray frontSideFilledBezierPathArray dependsFrom
     self.mCenterX, self.mCenterY, self.mWidth, self.mHeight, self.mHoleDiameter,
     self.mShape, self.mStyle,
     self.mSlavePads.all.frontSideFilledBezierPath
  ;

  transient BezierPathArray backSideFilledBezierPathArray dependsFrom
     self.mCenterX, self.mCenterY, self.mWidth, self.mHeight, self.mHoleDiameter,
     self.mShape, self.mStyle,
     self.mSlavePads.all.backSideFilledBezierPath
  ;

   transient EBShape padNumberDisplay dependsFrom
     self.mCenterX, self.mCenterY, prefs.padNumberColor, self.mName, prefs.padNumberFont ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity SlavePadInDevice {
  toOne MasterPadInDevice mMasterPad inverse toMany mSlavePads  ;

  property @signature Int mCenterX default 0 ;
  property @signature Int mCenterY default 0 ;
  property @signature Int mWidth default 114_300 ;
  property @signature Int mHeight default 228_600 ;
  property @signature Int mHoleDiameter default 57_150 ;
  property @signature PadShape mShape default octo ;
  property @signature SlavePadStyle mStyle default traversing ;

  transient NSBezierPath frontSideFilledBezierPath dependsFrom
     self.mCenterX, self.mCenterY, self.mWidth, self.mHeight, self.mHoleDiameter,
     self.mShape, self.mStyle
  ;

  transient NSBezierPath backSideFilledBezierPath dependsFrom
     self.mCenterX, self.mCenterY, self.mWidth, self.mHeight, self.mHoleDiameter,
     self.mShape, self.mStyle
  ;

   transient EBShape padNumberDisplay dependsFrom
     self.mCenterX, self.mCenterY,
     prefs.padNumberFont, prefs.padNumberColor, self.mMasterPad.one.mName
  ;
 
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity SymbolTypeInDevice {
  toMany @signature SymbolInstanceInDevice mInstances inverse toOne mType ;
  toMany @signature SymbolPinTypeInDevice mPinTypes ;

  property @signature String mTypeName default "" ;
  property @signature Int mVersion default 0 ;
  property @signature Data mFileData default empty ;
  
  property @signature NSBezierPath mStrokeBezierPath default empty ;
  property @signature NSBezierPath mFilledBezierPath default empty ;
  
  transient String versionString dependsFrom self.mVersion ;

  transient Int instanceCount dependsFrom self.mInstances.count ;
  
  transient EBShape pinNameShape dependsFrom self.mPinTypes.all.nameShape ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity SymbolPinTypeInDevice { # (SymbolPinInDevice) {
  toMany SymbolPinInstanceInDevice mInstances inverse toOne mType  ;

  property @signature Int mXName default 0 ;
  property @signature Int mYName default 0 ;
  property @signature String mName default "" ;
  property @signature HorizontalAlignment mNameHorizontalAlignment default center ;
  property @signature Bool mPinNameIsDisplayedInSchematics default yes ;

  property @signature Int mXNumber default 0 ;
  property @signature Int mYNumber default 0 ;
  property @signature HorizontalAlignment mNumberHorizontalAlignment default center ;

  transient EBShape nameShape dependsFrom
     self.mXName, self.mYName, 
     self.mName, self.mNameHorizontalAlignment,
     self.mPinNameIsDisplayedInSchematics,
     prefs.pinNameFont
   ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

graphic entity SymbolInstanceInDevice {
  toOne SymbolTypeInDevice mType inverse toMany mInstances ;
  toMany SymbolPinInstanceInDevice mPinInstances inverse toOne mSymbolInstance ;
 
  property @signature String mInstanceName default "" ;
  property @signature Int mX default 0 ;
  property @signature Int mY default 0 ;

  transient EBShape objectDisplay dependsFrom
     self.mType.one.mStrokeBezierPath,
     self.mType.one.mFilledBezierPath,
     self.mType.one.pinNameShape,
     self.mPinInstances.all.numberShape,
     self.qualifiedName,
     self.mX, self.mY,
     prefs.symbolDrawingWidthMultipliedByTen,
     prefs.symbolColor
  ;

  transient String symbolTypeName dependsFrom self.mType.one.mTypeName ;

  transient String qualifiedName dependsFrom self.mInstanceName, self.mType.one.mTypeName ;

  transient EBShape selectionDisplay dependsFrom
     self.mType.one.mStrokeBezierPath,
     self.mType.one.mFilledBezierPath,
     self.qualifiedName,
     prefs.symbolDrawingWidthMultipliedByTen,
     self.mX, self.mY
  ;
  
  transient UnconnectedSymbolPinsInDevice unconnectedPins dependsFrom 
    self.mPinInstances.all.pinName,
    self.mPinInstances.all.isConnected,
    self.mInstanceName  ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity SymbolPinInstanceInDevice {
  toOne SymbolInstanceInDevice mSymbolInstance inverse toMany mPinInstances ;
  toOne SymbolPinTypeInDevice mType inverse toMany mInstances ;
  toOne PadProxyInDevice mPadProxy inverse toOne mPinInstance ;

  transient EBShape numberShape dependsFrom
     self.mType.one.mXNumber, self.mType.one.mYNumber, 
     self.mType.one.mNumberHorizontalAlignment,
     self.mPadProxy.one.mPadName,
     prefs.pinNameFont
   ;

  transient String pinName dependsFrom self.mType.one.mName ;

  transient String symbolName dependsFrom self.mSymbolInstance.one.mInstanceName ;

  transient Bool isConnected dependsFrom self.mPadProxy.none ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

entity PadProxyInDevice {
  toOne SymbolPinInstanceInDevice mPinInstance inverse toOne mPadProxy ;
  
  property @signature String mPadName default "" ;
  
  property @signature Bool mIsNC default no ;
  
  transient Bool isConnected dependsFrom self.mIsNC, self.mPinInstance.none ;
  
  transient String pinInstanceName dependsFrom self.mPinInstance.one.pinName ;
  
  transient String symbolName dependsFrom self.mPinInstance.one.symbolName ;
}

#———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————
