//———————————————————————————————————————————————————————————————————————————————————————————————————————————————————————

autoLayoutDocument AutoLayoutSymbolDocument root SymbolRoot stackView mDocumentMainView {

//----------------------------------------------------------------------------------------------------------------------
//   MAIN VIEW
//----------------------------------------------------------------------------------------------------------------------
 
  stackView mPageMasterView vStack { }
 
  stackView mDocumentMainView vStack set (spacing: 0) {
    hStack set (margins: 8) {
      vStack {
        SegmentedControlWithPages (documentView: stackView mPageMasterView, equalWidth: no, size: .regular) {
          addPage (title: "Symbol", tooltip: "Symbol Editor", pageView: stackView mSymbolPage)
          addPage (title: "Infos", tooltip: "Symbol Informations", pageView: stackView mInfosPage)
          $selectedPage root.selectedPageIndex
        }
        hStack { space StaticLabel (title: "Page", bold: no, size: .small) space }
      }
      vStack {
        SignatureField (size : .regular) { $signature signature }
        StaticLabel (title: "Signature", bold: no, size: .small) { set (alignment: .center)  expandableWidth () }
      }
      vStack {
        VersionField (size : .regular) { $version version $versionShouldChange versionShouldChange }
        StaticLabel (title: "Version", bold: no, size: .small) { set (alignment: .center) expandableWidth () }
      }
      space
      vStack {
        hStack { space ImageObserverView (size: .regular) { $image self.statusImage $tooltip self.statusMessage } space }
        StaticLabel (title: "Status", bold: no, size: .small) { set (alignment: .center) }
      }
    }
    separator
    stackViewRef mPageMasterView
  }

  transient CanariIssueArray issues dependsFrom root.issues, self.documentFileName ;
  transient NSImage statusImage dependsFrom self.issues ;
  transient String statusMessage dependsFrom self.issues ;
  transient MetadataStatus metadataStatus dependsFrom self.issues ;

  transient Bool noIssue dependsFrom self.issues ;
  transient NSImage segmentedControlSegmentIssueImage dependsFrom self.issues ;
  transient String segmentedControlSegmentIssueString dependsFrom self.issues ;

//----------------------------------------------------------------------------------------------------------------------
//   PAGE   SYMBOL
//----------------------------------------------------------------------------------------------------------------------

  arrayController #mSymbolObjectsController : root.symbolObjects ;

  transient NSImage addSegmentButtonImage dependsFrom prefs.symbolColor ;
  transient NSImage addBezierButtonImage dependsFrom prefs.symbolColor ;
  transient NSImage addOvalButtonImage dependsFrom prefs.symbolColor ;
  transient NSImage addSolidOvalButtonImage dependsFrom prefs.symbolColor ;
  transient NSImage addSolidRectButtonImage dependsFrom prefs.symbolColor ;
  transient NSImage addTextButtonImage dependsFrom prefs.symbolColor ;
  transient NSImage addPinButtonImage dependsFrom prefs.symbolColor ;

  stackView mSymbolPageInspectorMasterView : vStack ;

  stackView mSymbolPage hStack set (margins: 0) set (spacing: 0) {
    vStack set (width: 250) {
      vStack set (leftMargin: 20) set (rightMargin: 20) set (topMargin: 8) set (spacing: 12) {
        hStack set (spacing: 0) {
          DragSourceButton (tooltip: "Add Segment") [configurator addSymbolSegment] { $image self.addSegmentButtonImage }
          space
          DragSourceButton (tooltip: "Add Bezier") [configurator addSymbolBezier] { $image self.addBezierButtonImage }
          space
          DragSourceButton (tooltip: "Add Solid Oval") [configurator addSymbolSolidOval] { $image self.addSolidOvalButtonImage }
          space
          DragSourceButton (tooltip: "Add Oval") [configurator addSymbolOval] { $image self.addOvalButtonImage }
          space
          DragSourceButton (tooltip: "Add Solid Rect") [configurator addSymbolSolidRect] { $image self.addSolidRectButtonImage }
          space
          DragSourceButton (tooltip: "Add Text") [configurator addSymbolText] { $image self.addTextButtonImage }
          space
          DragSourceButton (tooltip: "Add Pin") [configurator addSymbolPin] { $image self.addPinButtonImage }
        }
     }
     separator
     vStack set (leftMargin: 20) set (rightMargin: 20) {
       SegmentedControlWithPages (documentView: stackView mSymbolPageInspectorMasterView, equalWidth: yes, size: .small)
          [outlet mSymbolInspectorSegmentedControl configurator symbolPageSegmentedControl] {
          expandableWidth ()
          addPage (title: "", tooltip: "Selected Object Inspector", pageView: stackView mSelectedObjectsInspectorView)
          addPage (title: "", tooltip: "Grid / Zoom Inspector", pageView: stackView mGridZoomInspectorView)
          addPage (title: "", tooltip: "Display Inspector (values stored in Application Preferences)", pageView: stackView mDisplayInspectorView)
          addPage (title: "", tooltip: "Issue Inspector", pageView: stackView mIssuesInspectorView)
          $selectedPage root.selectedInspector
          $segmentImage self.segmentedControlSegmentIssueImage (segmentIndex : 3)
          $segmentTitle self.segmentedControlSegmentIssueString (segmentIndex : 3)
         }
      }
      vStack set (leftMargin: 20) set (rightMargin: 20) set (bottomMargin: 20) set (spacing: 12) {
        view mSymbolPageInspectorMasterView
      }
    }
    separator
    GraphicView (minZoom: 10, maxZoom: 4000) [outlet mSymbolGraphicView configurator symbolGraphicView] {
      $horizontalFlip root.horizontalFlip
      $verticalFlip root.verticalFlip
      $graphicController #mSymbolObjectsController
      $gridStyle root.gridStyle
      $gridDisplayFactor root.gridDisplay
      $gridLineColor prefs.lineColorOfSymbolGrid
      $gridCrossColor prefs.crossColorOfSymbolGrid
      $zoom root.zoom
      $backColor prefs.symbolBackgroundColor
      $xPlacardUnit root.xPlacardUnit
      $yPlacardUnit root.yPlacardUnit
    }
  }
  
//----------------------------------------------------------------------------------------------------------------------
//   Selected objects inspector
//----------------------------------------------------------------------------------------------------------------------

  stackView mSelectedObjectsInspectorView vStack {
    ObjectInspectorView {
      addObjectInspector (forEntity: entity SymbolText, inspectorView: stackView mSymbolTextInspectorView)
      addObjectInspector (forEntity: entity SymbolPin, inspectorView: stackView mSymbolPinInspectorView)
      $graphicController #mSymbolObjectsController
    }
    space
  }

//--- Symbol Text inspector
  selectionController SymbolText #mSymbolTextSelectionController : #mSymbolObjectsController.selectedArray ;

  stackView mSymbolTextInspectorView vStack {
    hStack { space StaticLabel (title: "Text Inspector", bold: yes, size: .small) space }
    TwoColumnsGridView {
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Pin Name", bold: no, size: .small),
        right: TextField (width: 56, size: .small) { expandableWidth () $value #mSymbolTextSelectionController.text (sendContinously : yes) }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Alignment", bold: no, size: .small),
        right: CanariHorizontalAlignmentSegmentedControl (size: .small) { expandableWidth () $alignment #mSymbolTextSelectionController.horizontalAlignment}
      )
    }
  }


//--- Symbol Pin inspector
  selectionController SymbolPin #mSymbolPinSelectionController : #mSymbolObjectsController.selectedArray ;

  stackView mSymbolPinInspectorView vStack {
    hStack { space StaticLabel (title: "Pin Inspector", bold: yes, size: .small) space }
    TwoColumnsGridView {
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Value", bold: no, size: .small),
        right: TextField (width: 56, size: .small) { expandableWidth () $value #mSymbolPinSelectionController.name (sendContinously : yes) }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Name Alignment", bold: no, size: .small),
        right: CanariHorizontalAlignmentSegmentedControl (size: .small) { expandableWidth () $alignment #mSymbolPinSelectionController.nameHorizontalAlignment}
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Number Alignment", bold: no, size: .small),
        right: CanariHorizontalAlignmentSegmentedControl (size: .small) { expandableWidth () $alignment #mSymbolPinSelectionController.numberHorizontalAlignment}
      )
      add (single: hStack {
        Switch (title : "Pin is visible in schematics", size: .small) { $value #mSymbolPinSelectionController.pinNameIsDisplayedInSchematics}
        space
      })
    }
  }

//----------------------------------------------------------------------------------------------------------------------
//   Zoom and flip inspector
//----------------------------------------------------------------------------------------------------------------------

  stackView mGridZoomInspectorView vStack {
    hStack { space StaticMiniLabel (title: "(Stored in Document Preferences)") space }
    TwoColumnsGridView {
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Grid Style", bold: no, size: .small),
        right: EnumPopUpButton (titles: enum GridStyle.popupTitles) { $selectedIndex root.gridStyle}
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Grid Display", bold: no, size: .small),
        right:  TaggedPopUpButton {
          add (title: "1 Step", withTag: 1)
          add (title: "2 Steps", withTag: 2)
          add (title: "4 Steps", withTag: 4)
          add (title: "5 Steps", withTag: 5)
          add (title: "8 Steps", withTag: 8)
          add (title: "10 Steps", withTag: 10)
          $selectedTag root.gridDisplay
        }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Grid Step", bold: no, size: .small),
        right: StaticLabel (title: "25 mils", bold: yes, size: .small) { set (alignment: .left) }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Flip", bold: no, size: .small),
        right: Switch (title: "Horizontal", size: .small) { $value root.horizontalFlip }
      )
      addFirstBaseLineAligned (
        left: FlexibleSpace,
        right: Switch (title: "Vertical", size: .small) { $value root.verticalFlip }
      )
      addFirstBaseLineAligned (
        left: StaticLabels (left: "Placard", right: "X", bold: no, size: .small),
        right: CanariUnitPopUpButton (size: .small) { $unit root.xPlacardUnit }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Y", bold: no, size: .small),
        right: CanariUnitPopUpButton(size: .small) { $unit root.yPlacardUnit }
      )
    }
    space
  }

//----------------------------------------------------------------------------------------------------------------------
//   Display inspector
//----------------------------------------------------------------------------------------------------------------------

  stackView mDisplayInspectorView vStack {
    hStack { space StaticMiniLabel (title: "(Stored in Application Preferences)") space }
    TwoColumnsGridView {
      addCenterYAligned (
        left: StaticLabel (title: "Symbol", bold: no, size: .small),
        right: hStack { ColorWell { $color prefs.symbolColor (sendContinously:no) } space }
      )
      addCenterYAligned (
        left: StaticLabel (title: "Background", bold: no, size: .small),
        right: hStack { ColorWell { $color prefs.symbolBackgroundColor (sendContinously:no) } space }
      )
      addCenterYAligned (
        left: StaticLabel (title: "Cross Grid", bold: no, size: .small),
        right: hStack { ColorWell { $color prefs.crossColorOfSymbolGrid (sendContinously:no) } space }
      )
      addCenterYAligned (
        left: StaticLabel (title: "Line Grid", bold: no, size: .small),
        right: hStack { ColorWell { $color prefs.lineColorOfSymbolGrid (sendContinously:no) } space }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Line Width", bold: no, size: .small),
        right: TaggedPopUpButton {
          add (title: "0.5 Point", withTag: 5)
          add (title: "1.0 Point", withTag: 10)
          add (title: "1.5 Point", withTag: 15)
          add (title: "2.0 Points", withTag: 20)
          add (title: "2.5 Points", withTag: 25)
          $selectedTag prefs.symbolDrawingWidthMultipliedByTen
        }
      )
      addFirstBaseLineAligned (
        left: StaticLabel (title: "Pin Font", bold: no, size: .small),
        right: FontButton (size: .small) {$fontValue prefs.pinNameFont}
      )
    }
    space
  }

//----------------------------------------------------------------------------------------------------------------------
//   Issue inspector
//----------------------------------------------------------------------------------------------------------------------

  stackView mIssuesInspectorView vStack {
   hStack { space Label (bold: no, size: .small) { $title self.statusMessage } space }
   CanariIssueTableView [outlet mSymbolIssueTableView] { $issues self.issues }
  }

  linker issueTableViewToGraphicView (mSymbolIssueTableView, mSymbolGraphicView)

//----------------------------------------------------------------------------------------------------------------------
//   PAGE   INFOS
//----------------------------------------------------------------------------------------------------------------------

 stackView mInfosPage vStack set (leftMargin: 20) set (rightMargin: 20) set (bottomMargin: 20) set (topMargin: 8) {
    hStack {
      Button (title: "Reset Version and Signature", size: .small) { $run resetVersionAction }
      space
    }
    hStack {
      vStack { StaticLabel (title: "Comments", bold: no, size: .regular) space }
      TextView { $value root.comments }
    }
  }

  action resetVersionAction ;

}

//----------------------------------------------------------------------------*
